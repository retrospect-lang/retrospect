// Compute digits of pi; algorithm from
// https://benchmarksgame-team.pages.debian.net/benchmarksgame/description/pidigits.html

spigot = initialSpigot
for _ in 1 .. n sequential spigot {
  digits << nextDigit(spigot=)
} collect {
  digits =| save
}
return digits

function initialSpigot() = {
  z: transform(1, 0, 0, 1),  // state
  k: 1                       // next input
}

// Crank spigot until we get the next digit, and return it
function nextDigit(spigot=) {
  for sequential spigot {
    digit = extract(spigot.z, 3)
    if digit == extract(spigot.z, 4) {
      break {
        spigot.z = compose(transform(10, -10 * digit, 0, 1), spigot.z)
        return digit
      }
    }
    spigot.z = compose(spigot.z, nextTransform(spigot.k))
    spigot.k += 1
  }
}

function transform(q, r, s, t) = {
    q: bigInt(q),
    r: bigInt(r),
    s: bigInt(s),
    t: bigInt(t)
  }

function nextTransform(k) = transform(k, 4 * k + 2, 0, 2 * k + 1)

function extract({q, r, s, t}, j) {
  j = bigInt(j)
  return asInt(div(q * j + r, s * j + t))
}

function compose(t1, t2) = {
    q: t1.q * t2.q,
    r: t1.q * t2.r + t1.r * t2.t,
    s: t1.s * t2.q + t1.t * t2.s,
    t: t1.s * t2.r + t1.t * t2.t
  }



// The rest of this file is an implementation of BigInt arithmetic.

// A BigInt's representation is { signum, mag }, where
//   - signum is 0, 1, or -1
//   - mag is a (little endian, minimal length) array of U32.
compound BigInt

// Returns the BigInt representing i.
function bigInt(Integer i) {
  if i == 0 {
    return BigInt_({signum: 0, mag: []})
  } else if i < 0 {
    signum = -1
    // Note that if i is -2^31 this will turn it into a double, which u32() will
    // then turn back into an int (without losing anything but a little
    // performance).  If we wanted to avoid that we could do something like
    //     uAdd(u32(-(1+i)), u32(1)).low
    i = -i
  } else {
    signum = 1
  }
  return BigInt_({signum, mag: [u32(i)]})
}

function bigZero() = bigInt(0)

// Returns the Integer with the same value as the given BigInt.
// Errors if x does not correspond to an Integer.
method asInt(BigInt x) {
  if x_.signum == 0 {
    return 0
  }
  [result] = x_.mag
  result = number(result)
  // asInt(number) will error if our result isn't an Integer.
  return asInt(x_.signum < 0 ? -result : result)
}

// Define the standard arithmetic operations over BigInts

method add(BigInt x, BigInt y) {
  if x_.signum == 0 {
    return y
  } else if y_.signum == 0 {
    return x
  } else if x_.signum == y_.signum {
    return BigInt_({signum: x_.signum, mag: addMags(x_.mag, y_.mag)})
  }
  cmp = compareMags(x_.mag, y_.mag)
  if cmp == 0 {
    return bigZero
  }
  mag = cmp > 0 ? subtractMags(x_.mag, y_.mag) : subtractMags(y_.mag, x_.mag)
  return BigInt_({signum: cmp == x_.signum ? 1 : -1, mag})
}

method negative(BigInt x) {
  return BigInt_({signum: -x_.signum, mag: x_.mag})
}

method subtract(BigInt x, BigInt y) {
  return x + negative(y)
}

method multiply(BigInt x, BigInt y) {
  signum = x_.signum * y_.signum
  if signum == 0 {
    return bigZero
  }
  // TODO: use more efficient algorithms (karatsuba, toom cook)
  // if both x and y are very large (size > 80)
  if size(x_.mag) == 1 {
    mag = multiplyMagByU32(y_.mag, x_.mag[1])
  } else if size(y_.mag) == 1 {
    mag = multiplyMagByU32(x_.mag, y_.mag[1])
  } else {
    mag = multiplyMags(x_.mag, y_.mag)
  }
  return BigInt_({signum, mag})
}

method div(BigInt x, BigInt y) = div(x, y, False)

method modulo(BigInt x, BigInt y) = div(x, y, True).r

// Returns {q, r}
function divWithRemainder(BigInt x, BigInt y) = div(x, y, True)

// Returns {q, r} if withRemainder is True, otherwise just q
private function div(BigInt x, BigInt y, withRemainder) {
  // TODO: use a more efficient algorithm (burnikel ziegler) if y is very large
  // (ySize > 80) and x is even larger (xSize > ySize + 40)
  signum = x_.signum * y_.signum
  if signum == 0 {
    assert y_.signum != 0
    return withRemainder ? {q: x, r: x} : x
  }
  result = divMags(x_.mag, y_.mag, withRemainder)
  q = (size(result.q) == 0) ? bigZero : BigInt_({signum, mag: result.q})
  if y_.signum < 0 and size(result.r) != 0 {
    // We want floor division, not truncate-towards-zero
    q = q + bigInt(-1)
    // We also have to fix r, but we'll do that below.
  }
  if not withRemainder {
    return q
  } else if size(result.r) == 0 {
    return {q, r: bigZero}
  }
  if y_.signum > 0 {
    r = BigInt_({signum: 1, mag: result.r})
  } else {
    r = BigInt_({signum: -1, mag: subtractMags(y_.mag, result.r)})
  }
  return {q, r}
}

private function uAdd(i, j, k) {
  sum1 = uAdd(i, j)
  sum2 = uAdd(sum1.low, k)
  return {low: sum2.low, high: uAdd(sum1.high, sum2.high).low}
}

// Returns 0, 1, or -1
private function compareMags(m1, m2) {
  n = size(m1)
  n2 = size(m2)
  if n != n2 {
    return n < n2 ? -1 : 1
  }
  for i in reverse(1..n) sequential {
    m1_i = m1[i]
    m2_i = m2[i]
    if m1_i != m2_i {
      break {
        return m1_i < m2_i ? -1 : 1
      }
    }
  }
  return 0
}

// Add two arrays of U32
private function addMags(x, y) {
  // Ensure that x is at least as long as y
  if size(x) < size(y) {
    [x, y] = [y, x]
  }
  // Add y into x
  carry = u32(0)
  for i in 1..size(x) sequential carry, x {
    if i <= size(y) {
      y_i_plus_carry = uAdd(y[i], carry)
    } else if carry == 0 {
      break
    } else {
      y_i_plus_carry = {low: carry, high: u32(0)}
    }
    {low, high} = uAdd(x[i], y_i_plus_carry.low)
    x[i] = low
    // These are both zero or one, so don't need to worry about overflow
    carry = uAdd(high, y_i_plus_carry.high).low
  }
  if carry != 0 {
    x &= [carry]
  }
  return x
}

// Subtract one array of U32 (y) from another (x); x must represent a
// larger number.
private function subtractMags(x, y) {
  // borrow will be 0 or 1
  borrow = u32(0)
  for i in 1..size(x) sequential borrow, x {
    if i <= size(y) {
      y_i_plus_borrow = uAdd(y[i], borrow)
    } else if borrow == 0 {
      break
    } else {
      y_i_plus_borrow = {low: borrow, high: u32(0)}
    }
    {low, high} = uSubtract(x[i], y_i_plus_borrow.low)
    x[i] = low
    // y_i_plus_borrow.high is 0 or 1; high is -1 or 0
    borrow = uSubtract(y_i_plus_borrow.high, high).low
  }
  assert borrow == 0
  return dropLeadingZeros(x)
}

private function dropLeadingZeros(mag) {
  end = size(mag)
  for i in reverse(1 .. end) sequential {
    if mag[i] != 0 {
      break {
        if i < end {
          mag[i + 1 ..] = []
        }
        return mag
      }
    }
  }
  return []
}

private function multiplyMagByU32(mag, k) {
  if bitCount(k) == 1 {
    carry = shiftMagLeft(mag=, bitFirstOne(k))
  } else {
    carry = u32(0)
    for i in 1..size(mag) sequential carry, mag {
      t1 = uMultiply(mag[i], k)
      t2 = uAdd(t1.low, carry)
      mag[i] = t2.low
      carry = uAdd(t1.high, t2.high).low
    }
  }
  if carry != 0 {
    mag &= [carry]
  }
  return mag
}

// Returns carry
private function shiftMagLeft(mag=, n) {
  assert n < 32
  if n == 0 {
    return u32(0)
  }
  prev = u32(0)
  for i in 1..size(mag) sequential prev, mag {
    mag_i = mag[i]
    mag[i] = bitRotate({low: mag_i, high: prev}, n)
    prev = mag_i
  }
  return bitRotate({low: u32(0), high: prev}, n)
}

// Multiply two arrays of U32
private function multiplyMags(x, y) {
  xSize = size(x)
  z = newMatrix([xSize + size(y)], u32(0))
  for [j]: y_j in y sequential z {
    // Add x * y[j] to z[j..]
    carry = 0
    for [i]: x_i in x sequential carry, z {
      t1 = uMultiply(x_i, y_j)
      t2 = uAdd(t1.low, z[j + i - 1], carry)
      z[j + i - 1] = t2.low
      carry = uAdd(t1.high, t2.high).low
    }
    z[j + xSize] = carry
  }
  return dropLeadingZeros(z)
}

// Returns {q, r}; if withRemainder is False, r will be None
// Based on Knuth "Algorithm D", as implemented by Hank Warren in Hacker's Delight
private function divMags(u, v, withRemainder) {
  cmp = compareMags(u, v)
  if cmp < 0 {
    // numerator less than denominator
    return { q: [], r: withRemainder ? u : None }
  } else if cmp == 0 {
    // numerator equal to denominator
    return { q: [1], r: withRemainder ? [] : None }
  } else if size(v) == 1 {
    r = divMagByU32(u=, v[1])
    if withRemainder {
      r = (r == 0) ? [] : [r]
    } else {
      r = None
    }
    return { q: u, r }
  }
  // TODO: consider canceling powers of two if size(y) > 5
  vSize = size(v)
  uSize = size(u)
  qSize = uSize - vSize + 1

  // Normalize by shifting u & v left until the top bit of v is 1
  normalizeShift = 31 - bitLastOne(v[vSize])
  carry = shiftMagLeft(v=, normalizeShift)
  assert carry == 0
  uTop = shiftMagLeft(u=, normalizeShift)

  q = newMatrix([qSize], u32(0))
  vTop = v[vSize]
  vTop2 = v[vSize - 1]
  for j in 0 .. qSize-1 sequential q, u, uTop {
    // Each iteration of this loop computes q[qSize - j] by
    // dividing u[qSize-j .. uSize-j] & [uTop] (vSize + 1 words)
    // by v (vSize words),
    // and subtracting the product from u[qSize-j .. uSize-j].

    // D3: Calculate qHat, our guess at q
    qHat = qHat(uTop, u[uSize - j], u[uSize - j - 1], vTop, vTop2)
    // At this point qHat is either the correct q, or q+1
    // D4: Multiply and subtract
    if qHat == 0 {
      assert uTop == 0
      uTop = u[uSize - j]
      continue
    }
    uStart = qSize - j - 1
    borrow = u32(0)
    // Subtract qHat * v from u[qSize-j .. uSize-j]
    for [i]: v_i in v sequential borrow, u {
      p = uMultiply(qHat, v_i)
      p2 = uAdd(p.low, borrow)
      t = uSubtract(u[uStart + i], p2.low)
      u[uStart + i] = t.low
      borrow = uAdd(p.high, p2.high).low
      borrow = uSubtract(borrow, t.high).low
    }
    // Update uTop
    t = uSubtract(uTop, borrow)
    // D5: Test remainder (should be either 0 or -1)
    assert t.low == t.high
    if t.high != 0 {
      // D6: Add back (qHat was one too high)
      qHat = uSubtract(qHat, u32(1)).low
      carry = u32(0)
      for [i]: v_i in v sequential carry, u {
        t2 = uAdd(u[uStart + i], v_i, carry)
        u[uStart + i] = t2.low
        carry = t2.high
      }
      // That should carry, to cancel the earlier borrow
      assert carry == 1
    }
    q[qSize - j] = qHat
    uTop = u[uSize - j]
  }
  q = dropLeadingZeros(q)
  if not withRemainder {
    return { q, r: None }
  }
  // D8: Unnormalize the remainder
  for i in 1 .. vSize {
    high = (i < vSize) ? u[i + 1] : u32(0)
    r << bitRotate({low: u[i], high}, -normalizeShift)
  } collect {
    r =| save
  }
  return {q, r}
}

// Implements step D3 of Algorithm D
private function qHat(uTop, uTop2, uTop3, vTop, vTop2) {
  { q: {low: qHat, high: qHigh}, r: rHat } = uDivWithRemainder({low: uTop2, high: uTop}, vTop)
  // Adjust qHat downward if we overshot
  for sequential qHat, qHigh, rHat {
    // We'll accept this qHat if qHigh is 0 and qHat * vTop2 <= { low: uTop3, high: rHat }
    if qHigh == 0 {
      t = uMultiply(qHat, vTop2)
      if t.high < rHat or (t.high == rHat and t.low <= uTop3) {
        break
      }
    }
    // qHat was obviously too high; decrement it and check again
    if qHat == 0 {
      assert qHigh == 1
      qHigh = u32(0)
    }
    qHat = uSubtract(qHat, u32(1)).low
    {low: rHat, high: carry} = uAdd(rHat, vTop)
    // We also accept qHat if rHat overflows a word
    if carry != 0 {
      break
    }
  }
  return qHat
}

// Returns the remainder
private function divMagByU32(mag=, denom) {
  if denom == 1 {
    return u32(0)
  } else if size(mag) <= 2 {
    mag2 = (size(mag) == 1) ? u32(0) : mag[2]
    { q: {low: qLow, high: qHigh}, r } = uDivWithRemainder({low: mag[1], high: mag2}, denom)
    if qHigh != 0 {
      mag = [qLow, qHigh]
    } else if qLow != 0 {
      mag = [qLow]
    } else {
      mag = []
    }
    return r
  }
  r = 0
  for i in reverse(1 .. size(mag)) sequential mag, r {
    { q: {low: qLow, high: qHigh}, r } = uDivWithRemainder({low: mag[i], high: r}, denom)
    assert qHigh == 0
    mag[i] = qLow
  }
  return r
}

/* RUN (n=1000) CODEGEN(10) RETURNS
 [3, 1, 4, 1, 5, 9, 2, 6, 5, 3, 5, 8, 9, 7, 9, 3, 2, 3, 8, 4, 6, 2, 6, 4, 3,
  3, 8, 3, 2, 7, 9, 5, 0, 2, 8, 8, 4, 1, 9, 7, 1, 6, 9, 3, 9, 9, 3, 7, 5, 1,
  0, 5, 8, 2, 0, 9, 7, 4, 9, 4, 4, 5, 9, 2, 3, 0, 7, 8, 1, 6, 4, 0, 6, 2, 8,
  6, 2, 0, 8, 9, 9, 8, 6, 2, 8, 0, 3, 4, 8, 2, 5, 3, 4, 2, 1, 1, 7, 0, 6, 7,
  9, 8, 2, 1, 4, 8, 0, 8, 6, 5, 1, 3, 2, 8, 2, 3, 0, 6, 6, 4, 7, 0, 9, 3, 8,
  4, 4, 6, 0, 9, 5, 5, 0, 5, 8, 2, 2, 3, 1, 7, 2, 5, 3, 5, 9, 4, 0, 8, 1, 2,
  8, 4, 8, 1, 1, 1, 7, 4, 5, 0, 2, 8, 4, 1, 0, 2, 7, 0, 1, 9, 3, 8, 5, 2, 1,
  1, 0, 5, 5, 5, 9, 6, 4, 4, 6, 2, 2, 9, 4, 8, 9, 5, 4, 9, 3, 0, 3, 8, 1, 9,
  6, 4, 4, 2, 8, 8, 1, 0, 9, 7, 5, 6, 6, 5, 9, 3, 3, 4, 4, 6, 1, 2, 8, 4, 7,
  5, 6, 4, 8, 2, 3, 3, 7, 8, 6, 7, 8, 3, 1, 6, 5, 2, 7, 1, 2, 0, 1, 9, 0, 9,
  1, 4, 5, 6, 4, 8, 5, 6, 6, 9, 2, 3, 4, 6, 0, 3, 4, 8, 6, 1, 0, 4, 5, 4, 3,
  2, 6, 6, 4, 8, 2, 1, 3, 3, 9, 3, 6, 0, 7, 2, 6, 0, 2, 4, 9, 1, 4, 1, 2, 7,
  3, 7, 2, 4, 5, 8, 7, 0, 0, 6, 6, 0, 6, 3, 1, 5, 5, 8, 8, 1, 7, 4, 8, 8, 1,
  5, 2, 0, 9, 2, 0, 9, 6, 2, 8, 2, 9, 2, 5, 4, 0, 9, 1, 7, 1, 5, 3, 6, 4, 3,
  6, 7, 8, 9, 2, 5, 9, 0, 3, 6, 0, 0, 1, 1, 3, 3, 0, 5, 3, 0, 5, 4, 8, 8, 2,
  0, 4, 6, 6, 5, 2, 1, 3, 8, 4, 1, 4, 6, 9, 5, 1, 9, 4, 1, 5, 1, 1, 6, 0, 9,
  4, 3, 3, 0, 5, 7, 2, 7, 0, 3, 6, 5, 7, 5, 9, 5, 9, 1, 9, 5, 3, 0, 9, 2, 1,
  8, 6, 1, 1, 7, 3, 8, 1, 9, 3, 2, 6, 1, 1, 7, 9, 3, 1, 0, 5, 1, 1, 8, 5, 4,
  8, 0, 7, 4, 4, 6, 2, 3, 7, 9, 9, 6, 2, 7, 4, 9, 5, 6, 7, 3, 5, 1, 8, 8, 5,
  7, 5, 2, 7, 2, 4, 8, 9, 1, 2, 2, 7, 9, 3, 8, 1, 8, 3, 0, 1, 1, 9, 4, 9, 1,
  2, 9, 8, 3, 3, 6, 7, 3, 3, 6, 2, 4, 4, 0, 6, 5, 6, 6, 4, 3, 0, 8, 6, 0, 2,
  1, 3, 9, 4, 9, 4, 6, 3, 9, 5, 2, 2, 4, 7, 3, 7, 1, 9, 0, 7, 0, 2, 1, 7, 9,
  8, 6, 0, 9, 4, 3, 7, 0, 2, 7, 7, 0, 5, 3, 9, 2, 1, 7, 1, 7, 6, 2, 9, 3, 1,
  7, 6, 7, 5, 2, 3, 8, 4, 6, 7, 4, 8, 1, 8, 4, 6, 7, 6, 6, 9, 4, 0, 5, 1, 3,
  2, 0, 0, 0, 5, 6, 8, 1, 2, 7, 1, 4, 5, 2, 6, 3, 5, 6, 0, 8, 2, 7, 7, 8, 5,
  7, 7, 1, 3, 4, 2, 7, 5, 7, 7, 8, 9, 6, 0, 9, 1, 7, 3, 6, 3, 7, 1, 7, 8, 7,
  2, 1, 4, 6, 8, 4, 4, 0, 9, 0, 1, 2, 2, 4, 9, 5, 3, 4, 3, 0, 1, 4, 6, 5, 4,
  9, 5, 8, 5, 3, 7, 1, 0, 5, 0, 7, 9, 2, 2, 7, 9, 6, 8, 9, 2, 5, 8, 9, 2, 3,
  5, 4, 2, 0, 1, 9, 9, 5, 6, 1, 1, 2, 1, 2, 9, 0, 2, 1, 9, 6, 0, 8, 6, 4, 0,
  3, 4, 4, 1, 8, 1, 5, 9, 8, 1, 3, 6, 2, 9, 7, 7, 4, 7, 7, 1, 3, 0, 9, 9, 6,
  0, 5, 1, 8, 7, 0, 7, 2, 1, 1, 3, 4, 9, 9, 9, 9, 9, 9, 8, 3, 7, 2, 9, 7, 8,
  0, 4, 9, 9, 5, 1, 0, 5, 9, 7, 3, 1, 7, 3, 2, 8, 1, 6, 0, 9, 6, 3, 1, 8, 5,
  9, 5, 0, 2, 4, 4, 5, 9, 4, 5, 5, 3, 4, 6, 9, 0, 8, 3, 0, 2, 6, 4, 2, 5, 2,
  2, 3, 0, 8, 2, 5, 3, 3, 4, 4, 6, 8, 5, 0, 3, 5, 2, 6, 1, 9, 3, 1, 1, 8, 8,
  1, 7, 1, 0, 1, 0, 0, 0, 3, 1, 3, 7, 8, 3, 8, 7, 5, 2, 8, 8, 6, 5, 8, 7, 5,
  3, 3, 2, 0, 8, 3, 8, 1, 4, 2, 0, 6, 1, 7, 1, 7, 7, 6, 6, 9, 1, 4, 7, 3, 0,
  3, 5, 9, 8, 2, 5, 3, 4, 9, 0, 4, 2, 8, 7, 5, 5, 4, 6, 8, 7, 3, 1, 1, 5, 9,
  5, 6, 2, 8, 6, 3, 8, 8, 2, 3, 5, 3, 7, 8, 7, 5, 9, 3, 7, 5, 1, 9, 5, 7, 7,
  8, 1, 8, 5, 7, 7, 8, 0, 5, 3, 2, 1, 7, 1, 2, 2, 6, 8, 0, 6, 6, 1, 3, 0, 0,
  1, 9, 2, 7, 8, 7, 6, 6, 1, 1, 1, 9, 5, 9, 0, 9, 2, 1, 6, 4, 2, 0, 1, 9, 8]
---
?0-----1-?--?0-1-0-1+∘---0-1+∘---0-?--?---2-?--?0--2-?-?0--1-3-?--?--?0-2+∘?0--1-0-1+∘-2-?-?1-3+∘-1-0-1+2+.+3+∘---4-?-?3-1∘-0--5-?0-5+∘2+∘.+5-2+.+5-2+.+3+4+.+3+5-2++5-2++3++2++5-2+.+3++5--?0-1-0-1+∘2+.+3+4+.+3+1-0-1+2+.+3+4+.+3+1∘5-0-5+∘2+.+5-2+∘++.+3+∘4+∘+3+2++++3++2+++.+3++5+0-1+2+.+3+4+.+3+1++2+.+3+4+.+3+1+?6-0-5∘?0-5+∘2+.+∘5-2+∘.+5-2+.+3∘5-3+∘--5-2++++3++5-2++5-2++3++5+?0-6-0-1+2+∘-+3+4+.+3+1++2+.+3+4+∘-3+1∘5+2+.++.++.+3+4+.+3+2++++3++2+++.+3++5+6+1+2+.+3+4+.+3+1∘+2+.+3+4+.+3+1+5+7bsrsrsr3+4+.+3+srsr8b9b8sr7srsr8sr5+6+1+7sr8s(9:1)r++7sr8s(9:1)r+5+7srsrsr8s(9:1)r7srsr8sr7srsr8sr+6+1+7sr8sr++7sr8sr+5+7srsrsr8sr7srsr8sr7srsr8sr+6+1+7sr8sr++7sr8sr+5+7srsrsr8sr7srsr8sr7srsr8sr+6+1+7sr8sr++7sr8sr+6+5+7srsrsr8s(9:1)r7srsr8sr7srsr8sr+0-1+7sr8s(9:1)r+10bs(7:1,8:1,9:1)r5+7srsrsr8s(9:1)r7srsr8sr7srsr8sr+6+10s(7:1,8:1,9:1)rs(7:1,8:1,9:1)r+5+7srsrs[?⊝?⊝⊝⊝⊝?⊝]8s[?⊝⊝⊝]7srsr8sr7srsr8sr+11+10s(7:1)7[.?⊝⊝⊝](8:1)8[?⊝⊝9s[?⊝⊝⊝?⊝⊝?⊝⊝?⊝]]10rs(7:1)7[..⊕](8:1)8[9s[..?⊝]8⊕]10[?⊝]5+7s[..⊕]s[..⊕]s[?⊚⊝⊝?⊝]8s[9s[..⊕]8⊕]7srsr8sr7srs[..⊕]8sr+6+10s(7:1)7[?⊚⊝.](8:1)8[9s[?⊚⊝]8⊕]10[?⊝?⊝⊝⊝?⊝⊝⊝?⊝⊝⊝⊝⊝⊝⊝⊝⊝?⊝?⊝]s(7:1)7[⊝⊝12-?---?⊝](8:1)8[9s[..⊕]8⊕]10[....?⊝⊝⊝⊝⊝⊝⊝?⊝?⊝]5+7s[12+..+⊕]s[+..+⊕]s[+..+⊕]8s[9s[..⊕]8⊕]7srsr8sr7srs[+..+⊕]8sr5+6+13b14b13s(7:1,10:1)7[12+..+⊕](8:1)8[?⊚⊝⊝]10[?⊝⊝.?⊝⊝.?⊝⊝...](7:1,10:1)7[+..∘⊕](8:1)8[..⊕]10[....15+...⊕](7:1,14:1)7[12+..+⊕](7:1)[+..+⊕](7:1)[+..+⊕](8:1)8[..⊕](7:4,8:1)7[+..+⊕](7:1,8:1,10:1)[+..+⊕](8:1)8[..⊕]10[?⊚..?⊝..15+...](7:1,10:1)7[12+..∘⊕](8:1)8[..⊕]10[....15+...⊕](7:1,14:1)7[12+..+⊕](7:1)[+..+⊕](7:1)[+..+⊕](8:1)8[..⊕](7:4,8:1)7[+..+⊕](7:1,8:1,10:1)[+..+⊕](8:1)8[..⊕]10[....15+...⊕](7:1,10:1)7[12+..+⊕](8:1)8[..⊕]10[....15+...⊕]13r14s(7:1)7[12+..+⊕](7:1)[+..+⊕16b17b](8:1,16:1,17:1)8[9s[..⊕]8⊕](8:2,16:4,17:1)14r11+13s(8:1,10:1,16:1,17:1)8[9s[..⊕]8⊕]10[....15+...⊕](8:1,10:1,16:1,17:1)8[9s[..⊕]8⊕]10[....+...⊕](8:1,14:1,16:3,17:3)8[9s[..⊕]8⊕](8:3,10:1,16:5,17:2)[..⊕]10[....+...⊕](8:1,10:1,16:1,17:1)8[..⊕]10[....+...⊕](8:1,14:1,16:3,17:3)8[..⊕](8:3,10:1,16:5,17:2)[..⊕]10[....+18bsr...10⊕](8:1,10:1,16:1,17:1)8[..⊕]10[....18sr...10⊕](14:1,16:1,17:1)17[?⊝⊝.](16:2,17:2)[?⊝⊝](8:1)8[..?⊚](8:3,10:1,16:5,17:2)[...⊕]10[?⊚..?⊝⊝..?⊝18⊝10⊝18sr10⊝18⊝10⊝18sr..10⊝?⊝⊝?⊝⊝?⊝](8:1,10:1,16:1,17:1)8[...⊕]10[.....18sr.sr....10⊕](14:1,16:1,17:1)17[.⊕](16:1,17:1)[.⊕](8:1,16:1,17:1)8[...⊕](8:1,16:4,17:1)17[.⊕](8:2,10:1,16:1,17:1)8[...⊕]10[?⊚⊝.....?⊝18sr...10⊝](10:1,16:1,17:1)17[?⊚⊝..](8:1)8[...⊕]10[......18sr....10⊕]13r14s(8:1,16:3,17:3)8[9s[...⊕]8⊕](8:2,16:4,17:1)14r11+13s(8:1,10:1,16:1,17:1)8[9s[...⊕]8⊕]10[......18sr....10⊕](10:1,16:1,17:1)17[...⊕](8:1)8[9s[...⊕]8⊕]10[......18sr....10⊕](8:1,14:1,16:3,17:3)8[9s[...⊕]8⊕](8:3,10:1,16:5,17:2)[9s[...⊕]8⊕]10[......18sr....10⊕](10:1,16:1,17:1)17[...⊕](8:1)8[9s[...⊕]8⊕]10[......18sr....10⊕](8:1,14:1,16:3,17:3)8[9s[...⊕]8⊕](8:3,10:1,16:5,17:2)[...⊕]10[......18sr....10⊕](10:1,16:1,17:1)17[...⊕](8:1)8[...⊕]10[......18sr....10⊕]13r14s(8:1,16:3,17:3)8[9s[...⊕]8⊕](8:2,16:4,17:1)14r+13s(8:1,10:1,16:1,17:1)8[9s[...⊕]8⊕]10[......18sr....10⊕](10:1,16:1,17:1)17[...⊕](8:1)8[9s[...⊕]8⊕]10[......18sr....10⊕](8:1,14:1,16:3,17:3)8[9s[...⊕]8⊕](8:3,10:1,16:5,17:2)[9s[...⊕]8⊕]10[......18sr....10⊕](10:1,16:1,17:1)17[...⊕](8:1)8[9s[...⊕]8⊕19b]10[......18sr....10⊕](9:1,14:1,16:3,17:3,19:1)9[...⊕20b](10:1,16:5,17:2,19:3)10[......18sr....10⊕](10:1,16:1,17:1)17[...⊕](19:1)10[......18sr....10⊕](14:1,16:3,17:3,19:1)19[?⊝?⊝⊝⊝⊝?⊝](10:1,16:5,17:2,19:3)10[.......18sr?10⊚18sr.....](10:1,16:1,17:1)17[...⊕](19:1)10[.......18sr.sr.....10⊕]13r14s(16:1,17:1)17[.⊕](16:2,17:2)[.⊕](16:4,17:1,19:3,20:1)14r+13s(10:1,16:1,17:1,19:1,20:1)20[?⊝?⊝]10[......18sr....10⊕](10:1,16:1,17:1)17[....⊕](19:1,20:1)10[.......18sr.sr.....10⊕](14:1,16:7,17:4,19:2,20:1)17[.⊕](10:1,16:1,17:1,19:2,20:1)10[........18sr.....10⊕](10:1,16:1,17:1)17[....⊕](19:1,20:1)10[........18sr.....10⊕](10:1,14:1,16:8,17:5,19:4,20:1)[........18sr.....10⊕](10:1,16:1,17:1)17[....⊕](19:1)10[........18sr.....10⊕](10:1,14:1,16:8,17:5,19:4)[........18sr.....10⊕](10:1,16:1,17:1)17[....⊕](19:1)10[........18sr.....10⊕](10:1,14:1,16:8,17:5,19:4)[........18sr.....10⊕](10:1,16:1,17:1)17[....⊕](19:1)10[........18sr.....10⊕]13r14s(16:7,17:4,19:3,20:1)r+13s(10:1,16:1,17:1,19:1,20:1)10[........18sr.....10⊕](10:1,16:1,17:1)17[....⊕](19:1,20:1)10[........18sr.....10⊕](10:1,14:1,16:8,17:5,19:4,20:2)[........18sr.....10⊕](10:1,16:1,17:1)17[....⊕](19:1,20:1)10[........18sr.....10⊕](14:1,16:2,17:2)17[.⊕](16:1,17:1)[.⊕](16:4,17:1,19:2,20:1)[.⊕21b](10:1,16:1,19:2,21:1)10[..........18sr......10⊕](10:1,16:1,21:1)21[?⊝?⊝.](19:1)10[..........18sr......10⊕]13r14s(16:7,19:3,20:1,21:4)r+13s(10:1,16:1,19:1,20:1,21:1)20[⊕]10[?⊚](10:1,16:1,19:1,20:1,21:1)[..........18sr......10⊕](10:1,14:1,16:8,19:4,20:2,21:5)[..........18sr......10⊕](10:1,16:1,19:1,20:1,21:1)[..........18sr......10⊕](10:1,14:1,16:8,19:4,20:1,21:5)[..........18sr......10⊕](10:1,16:1,19:1,21:1)[..........18sr......10⊕](10:1,14:1,16:8,19:4,21:5)[..........18sr......10⊕](10:1,16:1,19:1,21:1)[..........18sr......10⊕]13r14s(16:7,19:3,20:1,21:4)r+13s(10:1,16:1,19:1,20:1,21:1)10[..........18sr......10⊕](10:1,16:1,19:1,20:1,21:1)[..........18sr......10⊕](10:1,14:1,16:8,19:4,20:2,21:5)[..........18sr......10⊕](10:1,16:1,19:1,20:1,21:1)[..........18sr......10⊕](14:1,16:3,19:1,20:1,21:3)20[⊕](10:1,16:5,19:3,21:2)10[.....⊚22+++++18sr.sr.......10⊕](10:1,16:1,21:1)21[.⊕](19:1)10[.....+++++23bsr18sr.sr.......10⊕](10:1,14:1,16:8,19:4,21:5)[⊚24+++++23sr18sr10⊝25+++++.⊕](10:1,16:1,19:1,21:1)[24+++++26bsr23sr18sr25+++++27bsr.10⊕](10:1,14:1,16:8,19:4,21:5)[26sr23sr18sr27sr.10⊕](10:1,16:1,19:1,21:1)[26sr23sr18sr27sr.10⊕](10:1,14:1,16:8,19:4,21:5)[26sr23sr18sr27sr.10⊕](10:1,16:1,19:1,21:1)[26sr23sr18sr27sr.10⊕]13r14s(16:7,19:3,20:1,21:4)r?11∘0-13s(10:1,16:1,19:1,21:1)10[26sr23sr18sr27sr.10⊕](10:1,16:1,19:1,21:1)[26sr23sr18sr27sr.10⊕](10:1,14:1,16:8,19:4,21:5)[26sr23sr18sr27sr.10⊕](10:1,16:1,19:1,21:1)[26sr23sr18sr27sr.10⊕](14:1,16:3,19:1,21:3)19[?⊝⊝](10:1,16:5,19:3,21:2)10[26sr23sr18sr.sr27sr..10⊕](10:1,16:1,19:1,21:1)[26sr23sr18sr.sr27sr..10⊕]13r14s(16:7,19:3,20:1,21:4)r11+13s(10:1,16:1,19:1,20:1,21:1)20[⊕]10[26sr23sr18sr27sr.10⊕](10:1,16:1,21:1)21[.⊕](19:1,20:1)10[26sr23sr18sr.sr27sr..10⊕](10:1,14:1,16:8,19:4,20:2,21:5)[26sr23sr18sr27sr.10⊕](10:1,16:1,19:1,20:1,21:1)[26sr23sr18sr27sr.10⊕](10:1,14:1,16:8,19:4,20:1,21:5)[26sr23sr18sr27sr.10⊕](10:1,16:1,19:1,21:1)[26sr23sr18sr27sr.10⊕](10:1,14:1,16:8,19:4,21:5)[26sr23sr18sr27sr.10⊕](10:1,16:1,19:1,21:1)[26sr23sr18sr27sr.10⊕28b]13r14s(16:7,19:3,20:1,21:4)r+13s(14:2,16:19,19:11,20:6,21:13,28:5)19[.⊕](16:1,19:1,21:1,28:1)[.⊕](14:3,16:27,19:15,21:18,28:6)13r14s(16:7,19:3,20:1,21:4)r+13s(14:3,16:29,19:17,20:6,21:20,28:8)r14s(16:7,19:3,20:1,21:4)r+13s(14:3,16:29,19:17,20:6,21:20,28:8)r14s(16:7,19:3,20:1,21:4)r+13s(14:1,16:5,19:3,20:3,21:5,28:2)20[⊕](16:5,19:3,21:2,28:1)19[?⊚20s[?⊚]]28[?⊝18sr.sr27sr..](16:1,21:1,28:1)21[.⊕](19:1)19[20s[.⊕]19⊕]28[18sr.sr27sr..28⊕](14:1,16:9,19:5,20:1,21:6,28:2)13r14s(16:7,19:3,20:1,21:4)r+13s(14:4,16:38,19:22,20:6,21:26,28:10)r14s(16:7,19:3,20:1,21:4)r+13s(16:1,19:1,20:1,21:1,28:1)28[?⊚](14:3,16:28,19:16,20:5,21:19,28:7)13r14s(16:7,19:3,20:1,21:4)r+13s(14:1,16:5,19:3,20:3,21:5,28:2)20[⊕](16:5,19:3,21:2,28:1)19[20s[.⊚⊕]19⊕](16:1,21:1,28:1)21[.⊕](19:1)19[20s[.29+⊕]19⊕](14:4,16:36,19:20,20:1,21:24,28:8)13r14s(16:7,19:3,20:1,21:4)r11+13s(14:1,16:5,19:3,20:3,21:5,28:2)20[⊕](16:5,19:3,21:2,28:1)19[20s[?⊚29∘⊝]19⊕](16:1,21:1,28:1)21[.⊕](19:1)19[20s[⊕]19⊕](14:1,16:9,19:5,20:1,21:6,28:2)13r14s(16:7,19:3,20:1,21:4)r11+30bs(14:4,16:37,19:21,20:8,21:25,28:9)20[⊕](16:1,21:1,28:1)21[.⊕](19:1,20:1)20[⊕](14:11,16:99,19:55,20:21,21:66,28:22)19[.⊕](14:1,16:8,19:4,20:2,21:5,28:1)20[⊕](14:10,16:91,19:50,20:12,21:61,28:21)21[.⊕](14:6,16:53,19:30,20:12,21:35,28:11)28[18sr.sr27sr..28⊕](16:1,21:1,28:1)21[.⊕](19:1)28[18sr.sr27sr..28⊕](14:1,16:8,19:4,20:2,21:5,28:1)20[⊕](16:1,19:1,20:1,21:1,28:1)28[18sr.sr27sr..28⊕](14:4,16:30,19:16,20:4,21:21,28:6)19[.⊕](14:10,16:90,19:50,20:8,21:60,28:20)20[⊕](16:6,19:4,20:2,21:3,28:2)30[?⊝0-14s(16:7,19:3,21:3)r11*30s(14:11,16:101,19:56,20:20,21:68,28:24)21[.⊕](14:1,16:8,19:5,20:2,21:5,28:1)20[⊕](14:4,16:37,19:20,20:5,21:25,28:9)21[.⊕](14:6,16:53,19:30,20:12,21:35,28:11)28[18sr.sr27sr..28⊕](16:1,19:1,21:1,28:1)[18sr.sr27sr..28⊕](14:5,16:39,19:21,20:6,21:27,28:8)19[20s[⊕]19⊕](16:5,19:3,21:2,28:1)28[18sr.sr27sr..28⊕](16:1,21:1,28:1)21[.⊕](19:1)28[18sr.sr27sr..28⊕](14:5,16:39,19:21,20:4,21:27,28:8)20[⊕](16:5,19:3,21:2,28:1)19[20s[⊕]19⊕]28[18sr.sr27sr..28⊕](16:1,21:1,28:1)21[.⊕](19:1)19[20s[.⊕]19⊕]28[18sr.sr27sr..28⊕](14:1,16:8,19:4,20:1,21:5,28:1)[18sr.sr27sr..28⊕](16:1,19:1,20:1,21:1,28:1)[18sr.sr27sr..28⊕](14:9,16:81,19:44,20:11,21:54,28:18)21[.⊕](14:34,16:306,19:171,20:49,21:204,28:68)30[14s(16:7,19:3,21:3)r30⊕s(14:1,16:5,19:3,20:3,21:5,28:2)20[⊕](16:5,19:3,21:2,28:1)19[20s[⊕]19⊕]28[18sr.sr27sr..28⊕](16:1,21:1,28:1)21[.⊕](19:1)19[20s[⊕]19⊕]28[18sr.sr27sr..28⊕](14:9,16:75,19:41,20:9,21:51,28:16)20[⊕](14:1,16:9,19:5,20:3,21:6,28:2)[⊕](16:5,19:3,21:2,28:1)19[20s[.⊕]19⊕](16:1,19:1,21:1,28:1)[20s[.⊕]19⊕](14:4,16:30,19:16,20:2,21:21,28:6)20[⊕](16:6,19:4,20:2,21:3,28:2)30[14s(16:7,19:3,21:3)r30⊕s(16:1,19:1,20:1,21:1,28:1)20[⊕31b](16:1,21:1,28:1)21[.⊕](14:5,16:45,19:25,21:30,28:10,31:7)[.⊕](14:4,16:35,19:20,21:23,28:7,31:5)28[18sr.sr27sr..28⊕](16:1,19:1,21:1,28:1)[18sr.sr27sr..28⊕](14:1,16:8,19:4,21:5,28:1,31:2)[18sr.sr27sr..28⊕](16:1,19:1,21:1,28:1,31:1)[18sr.sr27sr..28⊕](14:14,16:125,19:69,21:83,28:27,31:22)19[31sr19⊕](16:1,21:1,28:1)21[.⊕](19:1)19[31sr19⊕](14:5,16:39,19:21,21:27,28:8,31:7)[31sr19⊕](14:14,16:132,19:73,21:87,28:30,31:21)21[.⊕](14:4,16:36,19:21,21:24,28:8,31:3)30[14s(16:7,19:3,21:3)r30⊕s(14:5,16:46,19:26,21:31,28:11,31:10)19[31sr19⊕](16:1,21:1,28:1)21[.⊕](19:1)19[31sr19⊕](14:4,16:36,19:19,21:24,28:8,31:1)21[.⊕32b](14:10,16:89,19:50,28:19,31:16,32:59)19[31sr19⊕33b](14:6,16:55,28:13,31:5,32:37,33:31)30[14s(16:7,32:3,33:3)r30⊕s(14:7,16:64,28:15,31:13,32:43,33:36)28[18sr.sr27sr..28⊕](16:1,28:1,32:1,33:1)[18sr.sr27sr..28⊕](14:1,16:8,28:1,31:2,32:5,33:4)[18sr.sr27sr..28⊕34b](14:16,16:145,31:22,32:97,33:81,34:33)30[14s(16:7,32:3,33:3)r30⊕s(14:10,16:86,31:10,32:59,33:48,34:20)33[?⊝31sr](14:25,16:231,31:32,32:153,33:129,34:52)30[13⊝?30⊚0-?⊝-14s(16:7,32:3,33:3)r30⊝-?⊝-⊝-s(14:50,16:452,31:77,32:302,33:252,34:102)[14s(16:7,32:3,33:3)r30⊕s(14:37,16:335,31:56,32:224,33:187,34:76)[14s(16:7,32:3,33:3)r30⊕s(14:43,16:389,31:63,32:260,33:217,34:88)[14s(16:7,32:3,33:3)r30⊕s(14:20,16:182,31:30,32:122,33:102,34:42)[14s(16:7,32:3,33:3)r30⊕s(14:29,16:263,31:44,32:176,33:147,34:60)[14s(16:7,32:3,33:3)r30⊕s(14:17,16:155,31:23,32:104,33:87,34:36)[14s(16:7,32:3,33:3)r30⊕s(14:44,16:392,31:70,32:263,33:218,34:88)33[31sr33⊕](14:15,16:141,31:20,32:93,33:79,34:32)30[14s(16:7,32:3,33:3)r30⊕s(14:57,16:515,31:84,32:344,33:287,34:116)[14s(16:7,32:3,33:3)r30⊕s(14:20,16:182,31:30,32:122,33:102,34:42)[14s(16:7,32:3,33:3)r30⊕s(14:13,16:119,31:16,32:80,33:67,34:28)[14s(16:7,32:3,33:3)r30⊕35bs(14:444,16:3992,31:682,32:2656,33:2218,34:888)33[?⊚31sr](14:73,16:657,31:113,32:437,33:365,34:146)33[31sr33⊕](14:409,16:3681,31:588,32:2441,33:2045,34:818)[31sr33⊕](14:62,16:558,31:94,32:372,33:310,34:124)[31sr33⊕](14:247,16:2223,31:356,32:1476,33:1235,34:494)[31sr33⊕](14:15,16:135,31:24,32:90,33:75,34:30)[31sr33⊕](14:130,16:1169,31:205,32:779,33:649,34:260)32[?⊝⊝36++++++++++37bsr](14:84,16:757,31:127,32:504,33:421,34:168)33[31sr33⊕](14:64,16:576,31:94,32:382,33:320,34:128)[31sr33⊕](14:159,16:1431,31:226,32:949,33:795,34:318)[31sr33⊕](14:9,16:80,31:13,32:53,33:44,34:18)32[*37sr32⊕](14:190,16:1711,31:277,32:1136,33:951,34:380)33[31sr33⊕](14:73,16:657,31:111,32:437,33:365,34:146)[31sr33⊕](14:330,16:2970,31:495,32:1974,33:1650,34:660)[31sr33⊕](14:164,16:1476,31:236,32:980,33:820,34:328)[31sr33⊕](14:283,16:2546,31:396,32:1691,33:1414,34:566)32[*37sr32⊕](14:92,16:829,31:132,32:550,33:461,34:184)33[31sr33⊕](14:103,16:927,31:151,32:616,33:515,34:206)[31sr33⊕](14:91,16:819,31:128,32:542,33:455,34:182)[31sr33⊕](14:570,16:5134,31:854,32:3411,33:2852,34:1140)35[⊝0-]]]]]]]]]]]]]]]]]]-
  0: x (top level)
  1: x pidigits.r8t+3:38
  2: x pidigits.r8t+3:117
  3: x pidigits.r8t+3:93
  4: x pidigits.r8t+3:225
  5: x pidigits.r8t+3:43
  6: r LoopCore.IterateUnbounded // pidigits.r8t+3:16_0 _t0 = iterateUnbounded(loop@16, loop@16_state⸨spigot=spigot⸩) // pidigits.r8t+3:3_7 _t0, spigot = nextDigit(spigot) // LoopCore.Iterate.nextState // pidigits.r8t+3:2_4 _t0 = iterate(_t0, _t2, loop@2⸨digits_ro=digits_ro, _eKind=_t2⸩, loop@2_state⸨digits_rw=_t1, spigot=spigot⸩)
  7: multiply_x0 (2)
  8: add_x1 (3)
  9: subtractMags_x2 (4)
 10: extract_x3 (1)
 11: r LoopCore.Iterate // pidigits.r8t+3:2_4 _t0 = iterate(_t0, _t2, loop@2⸨digits_ro=digits_ro, _eKind=_t2⸩, loop@2_state⸨digits_rw=_t1, spigot=spigot⸩)
 12: x pidigits.r8t+3:259
 13: iterateUnbounded_r4 (6)
 14: compose_x5 (5)
 15: r LoopCore.IterateUnbounded // pidigits.r8t+3:405_3 _t0 = iterateUnbounded(loop@405⸨vTop2=vTop2, uTop3=uTop3, vTop=vTop⸩, loop@405_state⸨qHat=qHat, qHigh=qHigh, rHat=rHat⸩) // pidigits.r8t+3:349_8 qHat = qHat(uTop, _t0, _t1, vTop, vTop2) // LoopCore.Iterate.nextState // pidigits.r8t+3:342_60 _t0 = iterate(_t0, EnumerateValues, loop@342⸨uSize=uSize, vTop=vTop, vTop2=vTop2, qSize=qSize, v=v⸩, loop@342_state⸨q=q, u=u, uTop=uTop⸩) // pidigits.r8t+3:149_25 result = divMags(_t0, _t1, withRemainder) // pidigits.r8t+3:133_0 _t0 = div(x, y, False) // pidigits.r8t+3:40_6 _t0 = div(_t0, _t1)
 16: multiply_x6 (2)
 17: multiplyMagByU32_x7 (12)
 18: iterateUnbounded_r8 (15)
 19: add_x9 (3)
 20: subtractMags_x10 (4)
 21: multiplyMagByU32_x11 (12)
 22: r LoopCore.Iterate // pidigits.r8t+3:283_12 _t0 = iterate(_t0, EnumerateValues, loop@283⸨n=n⸩, loop@283_state⸨prev=prev, mag=mag⸩) // pidigits.r8t+3:337_52 uTop, u = shiftMagLeft(u, normalizeShift) // pidigits.r8t+3:149_25 result = divMags(_t0, _t1, withRemainder) // pidigits.r8t+3:133_0 _t0 = div(x, y, False) // pidigits.r8t+3:40_6 _t0 = div(_t0, _t1)
 23: iterate_r12 (22)
 24: r LoopCore.Iterate // pidigits.r8t+3:283_12 _t0 = iterate(_t0, EnumerateValues, loop@283⸨n=n⸩, loop@283_state⸨prev=prev, mag=mag⸩) // pidigits.r8t+3:335_47 carry, v = shiftMagLeft(v, normalizeShift) // pidigits.r8t+3:149_25 result = divMags(_t0, _t1, withRemainder) // pidigits.r8t+3:133_0 _t0 = div(x, y, False) // pidigits.r8t+3:40_6 _t0 = div(_t0, _t1)
 25: r LoopCore.Iterate // pidigits.r8t+3:360_22 _t0 = iterate(v, EnumerateWithKeys, loop@360⸨qHat=qHat, uStart=uStart⸩, loop@360_state⸨borrow=borrow, u=u⸩) // LoopCore.Iterate.nextState // pidigits.r8t+3:342_60 _t0 = iterate(_t0, EnumerateValues, loop@342⸨uSize=uSize, vTop=vTop, vTop2=vTop2, qSize=qSize, v=v⸩, loop@342_state⸨q=q, u=u, uTop=uTop⸩) // pidigits.r8t+3:149_25 result = divMags(_t0, _t1, withRemainder) // pidigits.r8t+3:133_0 _t0 = div(x, y, False) // pidigits.r8t+3:40_6 _t0 = div(_t0, _t1)
 26: iterate_r13 (24)
 27: iterate_r14 (25)
 28: extract_x15 (1)
 29: r LoopCore.Iterate // pidigits.r8t+3:245_3 _t0 = iterate(_t0, EnumerateValues, loop@245⸨mag=mag⸩, Absent) // pidigits.r8t+3:240_15 _t0 = dropLeadingZeros(x)
 30: iterate_r16 (11)
 31: subtractMags_x17 (4)
 32: multiplyMagByU32_x18 (12)
 33: add_x19 (3)
 34: extract_x20 (1)
 35: iterate_r21 (11)
 36: r LoopCore.Iterate // pidigits.r8t+3:263_10 _t0 = iterate(_t0, EnumerateValues, loop@263⸨k=k⸩, loop@263_state⸨carry=carry, mag=mag⸩)
 37: iterate_r22 (36)
---
allocated=112453256/262024, peak=37562
$0 = *[]b0
$1 = *[]U32(i0)
*/

